# Plot  Angular Distribution

dt = 1;         # variação angular em graus
d2r = pi/180.;  # conversão graus para radianos
conv = dt*d2r;  #fator de conversão para índices da matriz (linha - coluna)
R = 1.0;        # raio da esfera

set terminal wxt size 640,640
set mapping cartesian
set view equal xyz
set xlabel 'x'
set ylabel 'y'
set zlabel 'z'

set pm3d depthorder #hidden3d

splot 'tW2TP.dat' every 2:2 matrix u (R*sin($1*conv)*cos($2*conv)):(R*sin($1*conv)*sin($2*conv)):(R*cos($1*conv)):($3) with pm3d t 'Gamma Angular Distribution'

pause -1 "enter for vector plot"

unset pm3d

splot 'tW2TP.dat' every 8:8 matrix u 0:0:0:($3*R*sin($1*conv)*cos($2*conv)):($3*R*sin($1*conv)*sin($2*conv)):($3*R*cos($1*conv)) with vectors lw 0.1 lc 'blue' t 'Gamma Angular Distribution'


# before end: unset

unset xlabel
unset ylabel
unset zlabel
unset mapping






